$PACKAGE APAP.IBS

*-----------------------------------------------------------------------------
* <Rating>297</Rating>
*-----------------------------------------------------------------------------
SUBROUTINE ITSS.IBSOLUTION.ARCIB.MIG.PWD
*---------------------------------------------------------------------------------------
*Modification History:
*DATE                 WHO                    REFERENCE                     DESCRIPTION
*25/10/2023         Suresh             R22 Manual Conversion                 Nochange
*----------------------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.EB.EXTERNAL.USER
    $INSERT I_F.ITSS.IBSOLUTION.USER
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.AA.UI.BEHAVIOUR
    $INSERT I_F.AA.USER.RIGHTS


    FN.AA.ARRANGEMENT = "F.AA.ARRANGEMENT"
    F.AA.ARRANGEMENT = ""
    CALL OPF(FN.AA.ARRANGEMENT,F.AA.ARRANGEMENT)

    FN.ITSS.IBSOLUTION.USER = "F.ITSS.IBSOLUTION.USER"
    F.ITSS.IBSOLUTION.USER = ""
    CALL OPF(FN.ITSS.IBSOLUTION.USER,F.ITSS.IBSOLUTION.USER)

    FN.ITSS.IBSOLUTION.USER.CUSTOMER = "F.ITSS.IBSOLUTION.USER.CUSTOMER"
    F.ITSS.IBSOLUTION.USER.CUSTOMER = ""
    CALL OPF(FN.ITSS.IBSOLUTION.USER.CUSTOMER,F.ITSS.IBSOLUTION.USER.CUSTOMER)

    CALL EB.CLEAR.FILE(FN.ITSS.IBSOLUTION.USER, F.ITSS.IBSOLUTION.USER)
    CALL EB.CLEAR.FILE(FN.ITSS.IBSOLUTION.USER.CUSTOMER, F.ITSS.IBSOLUTION.USER.CUSTOMER)

    FN.EB.EXTERNAL.USER = "F.EB.EXTERNAL.USER"
    F.EB.EXTERNAL.USER = ""
    CALL OPF(FN.EB.EXTERNAL.USER,F.EB.EXTERNAL.USER)

    Y.COUNT = 1
    Y.LIST.FINAL = ""
    SEL.COMMAND = "SELECT F.EB.EXTERNAL.USER WITH CHANNEL NE TELEFONO"
    CALL EB.READLIST(SEL.COMMAND,Y.LIST.FINAL,'',NO.OF.REC,ERR)
*    PRINT "SELECTED " : NO.OF.REC
    LOOP
        REMOVE K.USER FROM Y.LIST.FINAL SETTING POS
    WHILE K.USER:POS
        Y.COUNT++
        R.EB.EXTERNAL.USER = ""
        Y.ID.USER = K.USER
        READ R.EB.EXTERNAL.USER FROM F.EB.EXTERNAL.USER, Y.ID.USER  ELSE
            R.EB.EXTERNAL.USER = ''
            Y.ERROR = "EXT USER RECORD MISSING"
            RETURN
        END
        Y.CUSTOMER.ID = R.EB.EXTERNAL.USER<EB.XU.CUSTOMER>
        Y.ALIAS = R.EB.EXTERNAL.USER<EB.XU.NAME>
        Y.STATUS = R.EB.EXTERNAL.USER<EB.XU.STATUS>
        Y.PASSWORD = R.EB.EXTERNAL.USER<EB.XU.PASSWORD>
        Y.LAST.LOGIN = R.EB.EXTERNAL.USER<EB.XU.DATE.LAST.USE,1,1> : " " : R.EB.EXTERNAL.USER<EB.XU.TIME.LAST.USE,1,1>
        Y.ARRANGEMENT = R.EB.EXTERNAL.USER<EB.XU.ARRANGEMENT>
        Y.LIMIT.D = 0
*        CALL ITSS.GET.PROPTECTION.LIMIT(Y.ARRANGEMENT, Y.LIMIT.D)

        IBS.ID = Y.ID.USER
        R.IBS = ""
        R.IBS<1> = Y.ALIAS
        R.IBS<2> = Y.STATUS
        R.IBS<3> = Y.LAST.LOGIN
        R.IBS<4> = Y.PASSWORD
        R.IBS<5> = Y.LIMIT.D
        R.IBS<6> = Y.CUSTOMER.ID
        R.IBS<7> =  R.EB.EXTERNAL.USER<EB.XU.START.DATE>
        GOSUB GET.GROUP.ID
        R.IBS<8> = Y.PRODUCT
        R.IBS<9> = Y.USER.TYPE
        CALL F.WRITE(FN.ITSS.IBSOLUTION.USER,IBS.ID,R.IBS)

        IF MOD(Y.COUNT ,1000) EQ 0 THEN
            CALL JOURNAL.UPDATE("")
*            PRINT "EXECUTED : " : Y.COUNT
        END
        READ R.ITSS.IBSOLUTION.USER.CUSTOMER FROM F.ITSS.IBSOLUTION.USER.CUSTOMER, Y.CUSTOMER.ID  ELSE
            R.ITSS.IBSOLUTION.USER.CUSTOMER = ''
        END

        LOCATE Y.ID.USER IN R.ITSS.IBSOLUTION.USER.CUSTOMER SETTING POS.LOGIN ELSE POS.LOGIN = -1

        IF POS.LOGIN EQ -1 THEN
            R.ITSS.IBSOLUTION.USER.CUSTOMER<-1> = IBS.ID
            CALL F.WRITE(FN.ITSS.IBSOLUTION.USER.CUSTOMER,Y.CUSTOMER.ID,R.ITSS.IBSOLUTION.USER.CUSTOMER)
        END

    REPEAT

    CALL JOURNAL.UPDATE("")

RETURN

GET.GROUP.ID:
    Y.PRODUCT =""
    Y.USER.TYPE=""

    Y.ID.AA = R.EB.EXTERNAL.USER<EB.XU.ARRANGEMENT>
    R.AA.ARRANGEMENT = ""
    READ R.AA.ARRANGEMENT FROM F.AA.ARRANGEMENT , Y.ID.AA ELSE
        R.AA.ARRANGEMENT = ''
        Y.ERROR = "ARRANGEMENT RECORD MISSING"
        RETURN
    END

    Y.PRODUCT = R.AA.ARRANGEMENT<AA.ARR.PRODUCT>

    IF Y.PRODUCT EQ "PERSONAL" THEN
        Y.PRODUCT = "1-Transacciones"
        Y.USER.TYPE = "CLIENTE INDIVIDUAL"
        AA.ID = Y.ID.AA
        ARR.ID= Y.ID.AA
        PROPERTY.CLASS = "UI.BEHAVIOUR"
        AA.ID:='//AUTH'       ;*read the auth record first
        CALL AA.GET.ARRANGEMENT.CONDITIONS(AA.ID,PROPERTY.CLASS,'','',ARR.ID,PROPERTY.RECORD,ETEXT)
        CF$UI.BEHAVIOUR = RAISE(PROPERTY.RECORD)
        IF CF$UI.BEHAVIOUR<UI.BEHAV.FLOW.VALUE,1,1> EQ 'COS AI.REDO.PERSONAL.HOME.WITHOUT.TXN' THEN
            Y.PRODUCT = "4-Consultas"
        END
    END

    IF Y.PRODUCT EQ "CORPADMIN" THEN
        Y.PRODUCT = "7-Corporate Administrator"
        Y.USER.TYPE = "CORPORATE"
    END
    IF Y.PRODUCT EQ "CORPAUTH" THEN
        Y.PRODUCT = "6-Corporate User Authorizer"
        Y.USER.TYPE = "CORPORATE"
    END
    IF Y.PRODUCT EQ "CORPINPUT" THEN
        Y.PRODUCT = "5-Corporate User Inputter"
        Y.USER.TYPE = "CORPORATE"
    END


RETURN
