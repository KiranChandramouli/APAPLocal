SUBROUTINE LAPAP.RTN.OFS.CARGO
*------------------------------------------------------------------------------------------------------
* Description           : Rutina para disparar un mensaje ofs y cobrar el cargo por abono extraodinario
*                           si este se genera
*                         esta rutina esta atada a las versiones de caja para pago de abono extraodinario
*Versiones              : FUNDS.TRANSFER,REDO.MULTI.AA.ACRP.UPD.TR,FUNDS.TRANSFER,REDO.MULTI.AA.ACRP.UPD
* Developed On          : 24-03-2020
* Developed By          : APAP
* Development Reference : ET-5090
*--------------------------------------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.FUNDS.TRANSFER
    $INSERT I_F.REDO.H.REPORTS.PARAM
    $INSERT I_F.L.APAP.LIMITE.ABONADO
    $INSERT I_F.AA.BILL.DETAILS
    $INSERT I_F.ACCOUNT

    GOSUB LOARD.APLICATION
    Y.FT.ID = ID.NEW.LAST
    Y.CREDIT.ACCT.NO = R.NEW(FT.CREDIT.ACCT.NO)
    Y.IN.DEBIT.ACCT.NO = R.NEW(FT.DEBIT.ACCT.NO)
    Y.CUENTA.CAJA = Y.IN.DEBIT.ACCT.NO

    Y.CHAR.AMOUNT.POS = '';
    CALL GET.LOC.REF("FUNDS.TRANSFER","L.MON.CARG",Y.CHAR.AMOUNT.POS)
    Y.MONTO.CARGO = R.NEW(FT.LOCAL.REF)<1,Y.CHAR.AMOUNT.POS>

    Y.CHAR.ACC.POS = '';
    CALL GET.LOC.REF("FUNDS.TRANSFER","L.CHAR.ACC",Y.CHAR.ACC.POS)
    Y.CHAR.ACC = R.NEW(FT.LOCAL.REF)<1,Y.CHAR.ACC.POS>
    Y.CHAR.ACC = Y.CREDIT.ACCT.NO

    Y.FT.ORIGEN.POS = '';
    CALL GET.LOC.REF("FUNDS.TRANSFER","L.FT.ORIGEN",Y.FT.ORIGEN.POS)
    Y.FT.ORIGEN = R.NEW(FT.LOCAL.REF)<1,Y.FT.ORIGEN.POS>
    Y.FT.ORIGEN = Y.FT.ID

    GOSUB READ.ACCOUNT
    GOSUB SET.LIMITE
    GOSUB GET.REDO.H.REPORTS.PARAM
    IF Y.MONTO.CARGO GT 0 THEN
        GOSUB PROCESS.MESSAGE
    END
RETURN

*================
LOARD.APLICATION:
*================

    Y.FILE.NAME = "LIMITEABONADO":

    FN.CHK.DIR = "DMFILES"; F.CHK.DIR = ""
    CALL OPF(FN.CHK.DIR,F.CHK.DIR)

    FN.ST.L.APAP.LIMITE.ABONADO = "F.ST.L.APAP.LIMITE.ABONADO"; F.ST.L.APAP.LIMITE.ABONADO = ""
    CALL OPF (FN.ST.L.APAP.LIMITE.ABONADO,F.ST.L.APAP.LIMITE.ABONADO)

    FN.FUNDS.TRANSFER = "F.FUNDS.TRANSFER"; FV.FUNDS.TRANSFER = ""
    CALL OPF (FN.FUNDS.TRANSFER,FV.FUNDS.TRANSFER)

    FN.REDO.H.REPORTS.PARAM = "F.REDO.H.REPORTS.PARAM"; F.REDO.H.REPORTS.PARAM  = ""
    CALL OPF(FN.REDO.H.REPORTS.PARAM,F.REDO.H.REPORTS.PARAM)

    FN.ACCOUNT = "F.ACCOUNT"; FV.ACCOUNT = ""
    CALL OPF (FN.ACCOUNT, FV.ACCOUNT )

    FN.AA.BILL = "F.AA.BILL.DETAILS"; F.AA.BILL = ""
    CALL OPF (FN.AA.BILL,F.AA.BILL)

    Y.FECHA.ACTUAL = TODAY
RETURN

*============
READ.ACCOUNT:
*============
    ERR.ACCOUNT = "";  R.ACCOUNT = "" ; AA.ARR.ID = ""
    CALL F.READ (FN.ACCOUNT,Y.CREDIT.ACCT.NO,R.ACCOUNT,FV.ACCOUNT,ERR.ACCOUNT)
    AA.ARR.ID = R.ACCOUNT<AC.ARRANGEMENT.ID>
RETURN

*==========
SET.LIMITE:
*==========
    Y.FILE.NAME = "LIMITEABONADO":Y.CHAR.ACC
    R.CHK.DIR = '' ; CHK.DIR.ERROR = ''
    CALL F.READ(FN.CHK.DIR,Y.FILE.NAME,R.CHK.DIR,F.CHK.DIR,CHK.DIR.ERROR)
    IF NOT(R.CHK.DIR) THEN
        RETURN
    END

    LOOP
        REMOVE Y.REGISTRO FROM R.CHK.DIR SETTING PROCESO.POS
    WHILE Y.REGISTRO DO

        Y.REGISTRO.POS  = CHANGE(Y.REGISTRO,'|',@FM);

        Y.AA.ID         = Y.REGISTRO.POS<1>
        Y.CAL.ABO       = Y.REGISTRO.POS<2>
        Y.FT.ID         = Y.REGISTRO.POS<3>
        Y.LIMIT.EX      = Y.REGISTRO.POS<4>

        R.ST.LIMITE.ABONADO     = ""
        R.ST.LIMITE.ABONADO<ST.L.A61.N.PRESTAMO>   = Y.AA.ID
        R.ST.LIMITE.ABONADO<ST.L.A61.MONTO.EX>     = Y.CAL.ABO
        R.ST.LIMITE.ABONADO<ST.L.A61.TRANSACTION>  = Y.FT.ID
        R.ST.LIMITE.ABONADO<ST.L.A61.LIMIT.EX>     = Y.LIMIT.EX
        CALL F.WRITE(FN.ST.L.APAP.LIMITE.ABONADO,Y.AA.ID,R.ST.LIMITE.ABONADO)
    REPEAT
RETURN

*========================
GET.REDO.H.REPORTS.PARAM:
*========================
    ID.REPORT = "CARGO.EXTRACT"
    CALL F.READ(FN.REDO.H.REPORTS.PARAM,ID.REPORT,R.REDO.H.REPORTS.PARAM,F.REDO.H.REPORTS.PARAM,ERROR.REPORT.PARAM)
    Y.FIELD.NME.ARR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.NAME>
    Y.FIELD.VAL.ARR = R.REDO.H.REPORTS.PARAM<REDO.REP.PARAM.FIELD.VALUE>

    LOCATE "PL" IN Y.FIELD.NME.ARR<1,1> SETTING COD.POS THEN
        Y.PL = Y.FIELD.VAL.ARR<1,COD.POS>
        Y.PL = CHANGE(Y.PL,@SM,@FM)
        Y.PL = CHANGE(Y.PL,@VM,@FM)
        Y.PL = Y.PL<1>
    END

    LOCATE "TIPO.TRANSACION" IN Y.FIELD.NME.ARR<1,1> SETTING COD.POS3 THEN
        Y.TIPO.TRANSACION = Y.FIELD.VAL.ARR<1,COD.POS3>
        Y.TIPO.TRANSACION = CHANGE(Y.TIPO.TRANSACION,@SM,@FM)
        Y.TIPO.TRANSACION = CHANGE(Y.TIPO.TRANSACION,@VM,@FM)
        Y.TIPO.TRANSACION = Y.TIPO.TRANSACION<1>
    END
RETURN

*===============
PROCESS.MESSAGE:
*===============
    Y.TRANS.ID = ""
    Y.APP.NAME = "FUNDS.TRANSFER"
    Y.VER.NAME = Y.APP.NAME :",CHG.DISB"
    Y.FUNC = "I"
    Y.PRO.VAL = "PROCESS"
    Y.GTS.CONTROL = ""
    Y.NO.OF.AUTH = ""
    Y.OFS.QUEUE = ""
    R.FT = ""
    options = "OFS.CARGO" ; RESP = "" ; COMM = ""
    R.FT<FT.TRANSACTION.TYPE> = Y.TIPO.TRANSACION
    R.FT<FT.DEBIT.ACCT.NO> = Y.CUENTA.CAJA
    R.FT<FT.CREDIT.ACCT.NO> = Y.PL
    R.FT<FT.CREDIT.CURRENCY> = "DOP"
    R.FT<FT.CREDIT.AMOUNT> = Y.MONTO.CARGO
    R.FT<FT.LOCAL.REF,Y.CHAR.ACC.POS> = Y.CHAR.ACC
    R.FT<FT.LOCAL.REF,Y.FT.ORIGEN.POS> = Y.FT.ORIGEN
    R.FT<FT.ORDERING.BANK> = "APAP"
    CALL OFS.BUILD.RECORD(Y.APP.NAME,Y.FUNC,Y.PRO.VAL,Y.VER.NAME,Y.GTS.CONTROL,Y.NO.OF.AUTH,Y.TRANS.ID,R.FT,Y.OFS.QUEUE)
    CALL OFS.POST.MESSAGE(Y.OFS.QUEUE,RESP,options,COMM)

    DELETE F.CHK.DIR,Y.FILE.NAME
RETURN
END
