*-----------------------------------------------------------------------------
* <Rating>-52</Rating>
*-----------------------------------------------------------------------------
  SUBROUTINE REDO.VI.ATH.APPROVE

******************************************************************************
*  Company   Name    :Asociacion Popular de Ahorros y Prestamos
*  Developed By      :DHAMU.S
*  Program   Name    :REDO.VI.ATH.APPROVE
***********************************************************************************
*Description: * This is a version routine attached to approval version of ATH settelement process to
* review and approve the settlement
*****************************************************************************
*linked with:
*In parameter:
*Out parameter:
**********************************************************************
* Modification History :
*-----------------------
*DATE           WHO           REFERENCE         DESCRIPTION
*23.04.2012   BALAGURUNATHAN  ODR-2010-08-0469  INITIAL CREATION
*----------------------------------------------------------------------

$INSERT I_COMMON
$INSERT I_EQUATE
$INSERT I_F.FUNDS.TRANSFER
$INSERT I_F.ACCOUNT
$INSERT I_F.CURRENCY
$INSERT I_F.AC.LOCKED.EVENTS
$INSERT I_F.ATM.REVERSAL
$INSERT I_F.REDO.ATH.SETTLMENT
$INSERT I_F.REDO.APAP.H.PARAMETER
$INSERT I_F.REDO.ATH.STLMT.PARAM


  GOSUB INIT
  GOSUB PROCESS



  RETURN

*----------------------------------------------------------
INIT:
*----------------------------------------------------------


  TRANSACTION.ID = ''
  PROCESS = ''
  GTSMODE  =''
  OFSRECORD  = ''
  OFS.MSG.ID = ''
  OFS.ERR = ''
  OFS.STRING = ''
  OFS.ERR = ''

  FN.ATM.REVERSAL = 'F.ATM.REVERSAL'
  F.ATM.REVERSAL  =''
  CALL OPF(FN.ATM.REVERSAL,F.ATM.REVERSAL)

  FN.ACCOUNT ='F.ACCOUNT'
  F.ACCOUNT =''
  CALL OPF(FN.ACCOUNT,F.ACCOUNT)

  FN.AC.LOCKED.EVENTS='F.AC.LOCKED.EVENTS'
  F.AC.LOCKED.EVENTS=''
  CALL OPF(FN.AC.LOCKED.EVENTS,F.AC.LOCKED.EVENTS)

  FN.REDO.APAP.H.PARAMETER='F.REDO.APAP.H.PARAMETER'
  FN.REDO.ATH.STLMT.PARAM='F.REDO.ATH.STLMT.PARAM'


  RETURN
*----------------------------------------------------------
PROCESS:
*----------------------------------------------------------

  CARD.NUMBER=R.NEW(ATH.SETT.CARD.ACC.NO.TRAN)

  ATM.REVERSAL.ID=CARD.NUMBER:'.':R.NEW(ATH.SETT.AUTH.CODE)
  CALL F.READ(FN.ATM.REVERSAL,ATM.REVERSAL.ID,R.ATM.REVERSAL,F.ATM.REVERSAL,ATM.REVERSAL.ERR)

  Y.LOCK.ID=R.ATM.REVERSAL<AT.REV.TRANSACTION.ID>
  CALL F.READ(FN.AC.LOCKED.EVENTS,Y.LOCK.ID,R.AC.LOCKED.EVENTS,F.AC.LOCKED.EVENTS,ERR)

  IF R.ATM.REVERSAL EQ '' THEN
    ETEXT = "EB-NO.TXN.DETAILS"
    CALL STORE.END.ERROR
    RETURN
  END

  IF R.ATM.REVERSAL<AT.REV.TXN.REF> NE '' AND R.ATM.REVERSAL<AT.REV.TXN.REF>[1,4] NE 'ACLK' THEN
    ETEXT = 'EB-SETTLED.TXN'
    CALL STORE.END.ERROR
    RETURN
  END

  GOSUB PROCESS.NEXT

  RETURN


*----------------------------------------------------------
PROCESS.NEXT:
*----------------------------------------------------------
  GOSUB GET.LOCAL.REF
  Y.CHARGE.AMT=R.ATM.REVERSAL<AT.REV.TXN.CHRG>

  PARAMETER.ID = 'SYSTEM'
  CALL CACHE.READ(FN.REDO.APAP.H.PARAMETER,PARAMETER.ID,R.REDO.APAP.H.PARAMETER,PARAMETER.ERROR)

  TC.CODE = R.NEW(ATH.SETT.MSG.TYPE)


  Y.TC.CODE=R.REDO.APAP.H.PARAMETER<PARAM.TC.CODE>

  LOCATE TC.CODE IN Y.TC.CODE<1,1> SETTING POS1 THEN

    DR.ACCT=R.REDO.APAP.H.PARAMETER<PARAM.DR.ACCT,POS1>
    MODIFY.RTN=R.REDO.APAP.H.PARAMETER<PARAM.MODIFY.RTN,POS1>
    Y.FT.VERSION= R.REDO.APAP.H.PARAMETER<PARAM.FT.VERSION,POS1>
    Y.AC.LOCK.VERSION= R.REDO.APAP.H.PARAMETER<PARAM.AC.LCK.REV.VERSION,POS1>
  END

  IF MODIFY.RTN NE '' THEN
    OUT.PARAM<1>=DR.ACCT
    OUT.PARAM<2>=CR.ACCT
    CALL @MODIFY.RTN(OUT.PARAM)
  END

  Y.ACC.NO= R.ATM.REVERSAL<AT.REV.ACCOUNT.NUMBER>

  DR.ACCT=Y.ACC.NO

  R.FUNDS.TRANSFER<FT.DEBIT.ACCT.NO>=DR.ACCT
  R.FUNDS.TRANSFER<FT.TRANSACTION.TYPE>=R.ATM.REVERSAL<AT.REV.FTTC.ID>
  CALL F.READ(FN.ACCOUNT,DR.ACCT,R.DR.ACC,F.ACCOUNT,ACC.ERR)


  Y.DR.CCY=R.DR.ACC<AC.CURRENCY>
  Y.AVAILABLE.BALANCE=R.DR.ACC<AC.LOCAL.REF,POS.L.AC.AV.BAL>

  IF R.AC.LOCKED.EVENTS THEN
    Y.LOCKED.AMT = R.AC.LOCKED.EVENTS<AC.LCK.LOCKED.AMOUNT>
    Y.AVAILABLE.BALANCE=Y.AVAILABLE.BALANCE+Y.LOCKED.AMT
  END

  R.FUNDS.TRANSFER<FT.CREDIT.CURRENCY>=Y.DR.CCY   ;* Debit Account
  R.FUNDS.TRANSFER<FT.LOCAL.REF,POS.AT.UNIQUE.ID>=ATM.REVERSAL.ID
  R.FUNDS.TRANSFER<FT.LOCAL.REF,POS.POS.COND>=R.ATM.REVERSAL<AT.REV.POS.COND>
  R.FUNDS.TRANSFER<FT.LOCAL.REF,POS.BIN.NO>=CARD.NUMBER[1,6]
  R.FUNDS.TRANSFER<FT.LOCAL.REF,POS.AT.AUTH.CODE>=FIELD(ATM.REVERSAL.ID,'.',2)
  R.FUNDS.TRANSFER<FT.LOCAL.REF,POS.ATM.TERM.ID>=TRIMB(R.NEW(ATH.SETT.TERM.ID))
  R.FUNDS.TRANSFER<FT.LOCAL.REF,POS.L.STLMT.ID>=ID.NEW
  R.FUNDS.TRANSFER<FT.LOCAL.REF,POS.L.STLMT.APPL>='REDO.ATH.SETTLMENT'
  CR.AMT=R.NEW(ATH.SETT.REQUESTED.AMT.RD)


  R.FUNDS.TRANSFER<FT.CREDIT.AMOUNT>=CR.AMT       ;* Credit Amount
  R.FUNDS.TRANSFER<FT.DEBIT.VALUE.DATE>=TODAY
  R.FUNDS.TRANSFER<FT.CREDIT.VALUE.DATE>=TODAY

  Y.CHECK.BAL=CR.AMT+Y.CHARGE.AMT
  IF Y.AVAILABLE.BALANCE LT Y.CHECK.BAL THEN
    ETEXT = "EB-INSUFFICIENT"
    CALL STORE.END.ERROR
  END


  GOSUB OFS.POST

  RETURN


*----------------------------------------------------------------------
OFS.POST:
*----------------------------------------------------------------------

  APP.NAME = 'FUNDS.TRANSFER'
  OFSFUNCT='I'
  PROCESS  = ''
  OFSVERSION = 'FUNDS.TRANSFER,ATH.TC.APPROVE'
  GTSMODE = ''
  TRANSACTION.ID=''
  OFSRECORD = ''
  OFS.MSG.ID =''
  OFS.ERR = ''
  NO.OF.AUTH='0'
  CALL OFS.BUILD.RECORD(APP.NAME,OFSFUNCT,PROCESS,OFSVERSION,GTSMODE,NO.OF.AUTH,TRANSACTION.ID,R.FUNDS.TRANSFER,OFSRECORD1)
  OFS.SRC='REDO.VISA.OUTGOING'

  APP.NAME ='AC.LOCKED.EVENTS'
  OFSFUNCT='R'
  PROCESS  = ''
  OFSVERSION = Y.AC.LOCK.VERSION
  GTSMODE = ''
  TRANSACTION.ID=R.ATM.REVERSAL<AT.REV.TRANSACTION.ID>
  OFSRECORD = ''
  OFS.MSG.ID =''
  OFS.ERR = ''
  NO.OF.AUTH='0'
  R.OFS.AC.LOCK=''
  CALL OFS.BUILD.RECORD(APP.NAME,OFSFUNCT,PROCESS,OFSVERSION,GTSMODE,NO.OF.AUTH,TRANSACTION.ID,R.OFS.AC.LOCK,OFSRECORD2)
  OFS.STRING.FINAL=OFSRECORD2:FM:OFSRECORD1
  CALL OFS.POST.MESSAGE(OFS.STRING.FINAL,OFS.MSG.ID,OFS.SRC,OPTIONS)


  RETURN



*-----------------------------------------------------------
GET.LOCAL.REF:
*-----------------------------------------------------------

  LOC.REF.APPLICATION="FUNDS.TRANSFER":FM:"ACCOUNT"
  LOC.REF.FIELDS='AT.UNIQUE.ID':VM:'POS.COND':VM:'BIN.NO':VM:'AT.AUTH.CODE':VM:'ATM.TERM.ID':VM:'L.STLMT.ID':VM:'L.STLMT.APPL':FM:'L.AC.AV.BAL'
  LOC.REF.POS=''
  CALL MULTI.GET.LOC.REF(LOC.REF.APPLICATION,LOC.REF.FIELDS,LOC.REF.POS)
  POS.AT.UNIQUE.ID=LOC.REF.POS<1,1>
  POS.POS.COND=LOC.REF.POS<1,2>
  POS.BIN.NO=LOC.REF.POS<1,3>
  POS.AT.AUTH.CODE=LOC.REF.POS<1,4>
  POS.ATM.TERM.ID=LOC.REF.POS<1,5>
  POS.L.STLMT.ID=LOC.REF.POS<1,6>
  POS.L.STLMT.APPL=LOC.REF.POS<1,7>
  POS.L.AC.AV.BAL=LOC.REF.POS<2,1>
  RETURN

END
