SUBROUTINE DR.REG.GET.CUST.TYPE(R.CUSTOMER,ACCOUNT.ID,CUST.TYPE,CUST.IDEN)
*-------------------------------------------------------------------------
* Date              Author                    Description
* ==========        ====================      ============
* 06/10/2014        Ashokkumar.V.P            PACS00312712 - Common routine to get the Customer type and Identication number
* 20/11/2014        Ashokkumar.V.P            PACS00312712 - Reinitialized the R.CUSTOMER value.
*-------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.CUSTOMER
    $INSERT I_F.ACCOUNT

    GOSUB INIT
    GOSUB GET.VALUES
    IF CU.TIPO.CL EQ 'CLIENTE MENOR' THEN
        GOSUB GET.CUST.MINOR
    END
    GOSUB GET.CUST.IDEN
    GOSUB GET.CUST.TYPE
    R.CUSTOMER = YTEMP.CUSTOMER
RETURN

INIT:
*****
    YTEMP.CUSTOMER = R.CUSTOMER
    GOSUB INIT.V
    Y.FIELD.POS = ''; Y.APAP.INDUS.POS = ''; L.CU.CIDENT.POS = ''; L.CU.RNC.POS = ''
    L.CU.FOREIGN.POS = ''; L.CU.TIPO.CL.POS = ''
    FN.ACCOUNT = 'F.ACCOUNT'; F.ACCOUNT = ''
    FN.CUSTOMER = 'F.CUSTOMER'; F.CUSTOMER = ''
    FN.ACCOUNT.HST = 'F.ACCOUNT$HIS'; F.ACCOUNT.HST = ''
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)
    CALL OPF(FN.ACCOUNT.HST,F.ACCOUNT.HST)

    Y.APPLICATION = 'CUSTOMER'
    Y.FIELDS = 'L.APAP.INDUSTRY':@VM:'L.CU.TIPO.CL':@VM:'L.CU.CIDENT':@VM:'L.CU.RNC':@VM:'L.CU.FOREIGN'
    CALL MULTI.GET.LOC.REF(Y.APPLICATION,Y.FIELDS,Y.FIELD.POS)
    Y.APAP.INDUS.POS = Y.FIELD.POS<1,1>
    L.CU.TIPO.CL.POS = Y.FIELD.POS<1,2>
    L.CU.CIDENT.POS = Y.FIELD.POS<1,3>
    L.CU.RNC.POS = Y.FIELD.POS<1,4>
    L.CU.FOREIGN.POS = Y.FIELD.POS<1,5>
RETURN

INIT.V:
*******
    CUS.NATION = ''; CUS.GENDER = ''; CUS.RESID = ''; CU.TIPO.CL = ''; CUS.INDUST = ''; YCUS.CIDENT = ''
    YCUS.RNC = ''; YCUS.FOREIGN = ''; YCUS.LEGAL = ''; CUST.TYPE = ''; CUST.IDEN = ''
RETURN

GET.VALUES:
***********
    CUS.NATION = R.CUSTOMER<EB.CUS.NATIONALITY>
    CUS.GENDER = R.CUSTOMER<EB.CUS.GENDER>
    CUS.RESID = R.CUSTOMER<EB.CUS.RESIDENCE>
    CU.TIPO.CL = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.TIPO.CL.POS>
    CUS.INDUST = R.CUSTOMER<EB.CUS.LOCAL.REF,Y.APAP.INDUS.POS>
    YCUS.CIDENT = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.CIDENT.POS>
    YCUS.RNC = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.RNC.POS>
    YCUS.FOREIGN = R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.FOREIGN.POS>
    YCUS.LEGAL = R.CUSTOMER<EB.CUS.LEGAL.ID,1>
RETURN

GET.CUST.MINOR:
***************
    R.ACCOUNT = ''; ACCOUNT.ERR = ''; CUS.ID = ''; YACCOUNT.ID = ''
    CALL F.READ(FN.ACCOUNT,ACCOUNT.ID,R.ACCOUNT,F.ACCOUNT,ACCOUNT.ERR)
    IF NOT(R.ACCOUNT) THEN
        YACCOUNT.ID = ACCOUNT.ID; ACCOUNT.ERR = ''
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,YACCOUNT.ID,R.ACCOUNT,ACCOUNT.ERR)
    END
    IF NOT(R.ACCOUNT) THEN
        RETURN
    END
    YACCT.JOINT = R.ACCOUNT<AC.JOINT.HOLDER,1>
    CUS.ID = YACCT.JOINT
    GOSUB READ.CUSTOMER
    GOSUB INIT.V
    GOSUB GET.VALUES
RETURN

GET.CUST.IDEN:
**************
    BEGIN CASE
        CASE YCUS.CIDENT NE ''
            CUST.IDEN = YCUS.CIDENT[1,3]:'-':YCUS.CIDENT[4,7]:'-':YCUS.CIDENT[11,1]
        CASE YCUS.RNC NE ''
            CUST.IDEN = YCUS.RNC[1,1]:'-':YCUS.RNC[2,2]:'-':YCUS.RNC[4,5]:'-':YCUS.RNC[9,1]
        CASE YCUS.FOREIGN NE ''
            CUST.IDEN = CUS.NATION:YCUS.FOREIGN
        CASE 1
            CUST.IDEN = CUS.NATION:YCUS.LEGAL
    END CASE
RETURN

GET.CUST.TYPE:
**************
    BEGIN CASE
        CASE CUS.NATION EQ 'DO' AND CUS.GENDER EQ 'MALE' AND CU.TIPO.CL EQ 'PERSONA FISICA'
            CUST.TYPE = 'P3'
        CASE CUS.NATION NE 'DO' AND CUS.GENDER EQ 'MALE' AND CUS.RESID EQ 'DO' AND CU.TIPO.CL EQ 'PERSONA FISICA'
            CUST.TYPE = 'P4'
        CASE CUS.NATION EQ 'DO' AND CUS.GENDER EQ 'FEMALE' AND CU.TIPO.CL EQ 'PERSONA FISICA'
            CUST.TYPE = 'P5'
        CASE CUS.NATION NE 'DO' AND CUS.RESID EQ 'DO' AND CUS.GENDER EQ 'FEMALE' AND CU.TIPO.CL EQ 'PERSONA FISICA'
            CUST.TYPE = 'P6'
        CASE CUS.NATION NE 'DO' AND CUS.RESID NE 'DO' AND CUS.GENDER EQ 'MALE' AND CU.TIPO.CL EQ 'PERSONA FISICA'
            CUST.TYPE = 'P7'
        CASE CUS.NATION NE 'DO' AND CUS.RESID NE 'DO' AND CUS.GENDER EQ 'FEMALE' AND CU.TIPO.CL EQ 'PERSONA FISICA'
            CUST.TYPE = 'P8'
        CASE CUS.NATION EQ 'DO' AND CU.TIPO.CL EQ 'PERSONA JURIDICA'
            CUST.TYPE = 'E1'
        CASE CUS.NATION NE 'DO' AND CU.TIPO.CL EQ 'PERSONA JURIDICA' AND (CUS.INDUST LT '651000' AND CUS.INDUST NE '')
            CUST.TYPE = 'E2'
        CASE CUS.NATION NE 'DO' AND CU.TIPO.CL EQ 'PERSONA JURIDICA' AND CUS.INDUST GT '659990'
            CUST.TYPE = 'E2'
        CASE CUS.NATION NE 'DO' AND CU.TIPO.CL EQ 'PERSONA JURIDICA' AND (CUS.INDUST GE '651000' AND CUS.INDUST LE '659990')
            CUST.TYPE = 'E3'
    END CASE
RETURN

READ.CUSTOMER:
**************
    CUSTOMER.ERR = ''; R.CUSTOMER = ''
    CALL F.READ(FN.CUSTOMER,CUS.ID,R.CUSTOMER,F.CUSTOMER,CUSTOMER.ERR)
RETURN
END
