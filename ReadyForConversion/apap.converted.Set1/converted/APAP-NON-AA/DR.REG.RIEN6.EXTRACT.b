SUBROUTINE DR.REG.RIEN6.EXTRACT(REC.ID)
*----------------------------------------------------------------------------
* Company Name   : APAP
* Company Name   : APAP
* Developed By   : gangadhar@temenos.com
* Program Name   : DR.REG.RIEN6.EXTRACT.
* Date           : 3-May-2013
*----------------------------------------------------------------------------
* Description:
*------------
* This multi-thread job is meant for to extact the AZ.ACCOUNT in DOP and non DOP
*----------------------------------------------------------------------------
*
* Modification History :
* ----------------------
*   Date       Author              Modification Description
* 11/07/2014  V.P.Ashokkumar       PACS00312508 - Updated selection criteria, field length and fixed the field bug
* 15/10/2014  V.P.Ashokkumar       PACS00312508 - Updated new mapping changes for client minor and interest amount
* 20/05/2015  V.P.Ashokkumar       PACS00312508 - Added new fields to display in the report
*----------------------------------------------------------------------------

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_BATCH.FILES
    $INSERT I_TSA.COMMON
    $INSERT I_F.CUSTOMER
    $INSERT I_F.ACCOUNT
    $INSERT I_F.AZ.ACCOUNT
    $INSERT I_F.COMPANY
    $INSERT I_F.INDUSTRY
    $INSERT I_F.GROUP.DATE
    $INSERT I_F.GROUP.CREDIT.INT
    $INSERT I_F.EB.CONTRACT.BALANCES
    $INSERT I_F.RE.STAT.REP.LINE
*
    $INSERT I_DR.REG.RIEN6.EXTRACT.COMMON
    $INSERT I_F.DR.REG.RIEN6.PARAM
    $INSERT I_F.REDO.AZACC.DESC
*
    GOSUB MAIN.PROCESS
    GOSUB PROCESS
RETURN

MAIN.PROCESS:
*************
    BEGIN CASE
        CASE CONTROL.LIST<1,1> EQ 'TERM.DEPOSIT'
            GOSUB PROCESS
        CASE CONTROL.LIST<1,1> EQ 'ACCOUNT.CLOSE'
            GOSUB PROCESS
    END CASE
RETURN

PROCESS:
********
*
    R.AZ.ACCOUNT = ''; AZ.ACCOUNT.ERR = ''; R.ACCOUNT = ''; ACCOUNT.ERR = ''
    ERR.AZ.ACCOUNT = ''; YH.REC.ID = ''; ERR.ACCOUNT = ''
    CALL F.READ(FN.AZ.ACCOUNT,REC.ID,R.AZ.ACCOUNT,F.AZ.ACCOUNT,AZ.ACCOUNT.ERR)
    IF NOT(R.AZ.ACCOUNT) THEN
        YH.REC.ID = REC.ID
        CALL EB.READ.HISTORY.REC(F.AZ.ACCOUNT.HST,YH.REC.ID,R.AZ.ACCOUNT,ERR.AZ.ACCOUNT)
    END
    IF NOT(R.AZ.ACCOUNT) THEN
        RETURN
    END
    CALL F.READ(FN.ACCOUNT,REC.ID,R.ACCOUNT,F.ACCOUNT,ACCOUNT.ERR)
    IF NOT(R.ACCOUNT) THEN
        YH.REC.ID = REC.ID
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,YH.REC.ID,R.ACCOUNT,ERR.ACCOUNT)
    END
    Y.ALT.ACCT.TYPE = ''; Y.ALT.ACCT.ID = ''; Y.PREV.ACCOUNT = ''
    Y.ALT.ACCT.TYPE = R.ACCOUNT<AC.ALT.ACCT.TYPE>
    Y.ALT.ACCT.ID = R.ACCOUNT<AC.ALT.ACCT.ID>
    LOCATE 'ALTERNO1' IN Y.ALT.ACCT.TYPE<1,1> SETTING ALT.TYPE.POS THEN
        Y.PREV.ACCOUNT = Y.ALT.ACCT.ID<1,ALT.TYPE.POS>
    END ELSE
        Y.PREV.ACCOUNT = REC.ID
    END

    RECORD.STAT = ''; YAZ.OPEN.DATE = ''; CAT.VAL = ''; CCY.VAL = ''
    CUS.ID = ''; YROLL.DTE = ''; R.CUSTOMER = ''; CUSTOMER.ERRR = ''
    RECORD.STAT = R.ACCOUNT<AC.RECORD.STATUS>
    YAC.OPEN.DATE = R.ACCOUNT<AC.OPENING.DATE>
    CAT.VAL = R.AZ.ACCOUNT<AZ.CATEGORY>
    CCY.VAL = R.AZ.ACCOUNT<AZ.CURRENCY>
    CUS.ID = R.AZ.ACCOUNT<AZ.CUSTOMER>
    YROLL.DTE = R.AZ.ACCOUNT<AZ.ROLLOVER.DATE>
    CALL F.READ(FN.CUSTOMER,CUS.ID,R.CUSTOMER,F.CUSTOMER,CUSTOMER.ERR)
    GOSUB PROCESS.LCY
*
    IF CCY.VAL EQ LCCY THEN
        CALL F.WRITE(FN.DR.REG.RIEN6.WORKFILE, REC.ID, RETURN.MSG)
    END ELSE
        CALL F.WRITE(FN.DR.REG.RIEN6.WORKFILE.FCY,REC.ID,RETURN.MSG)
    END
RETURN

GET.INT.PAY.PERIOD:
******************
    SCH.POS = ''
    LOCATE 'I' IN R.AZ.ACCOUNT<AZ.TYPE.OF.SCHDLE,1> SETTING SCH.POS THEN
        SCH.TYPE = R.AZ.ACCOUNT<AZ.TYPE.OF.SCHDLE,SCH.POS>
        FRQ.VAL = R.AZ.ACCOUNT<AZ.FREQUENCY,SCH.POS>
        SCH.VAL = R.AZ.ACCOUNT<AZ.SCHEDULES>
        MAT.VAL = MAT.DATE
        BEGIN CASE
            CASE SCH.VAL EQ 'N'
                INT.PAY.PER = '99'
            CASE FRQ.VAL[9,3] EQ 'M12'
                INT.PAY.PER = '1'
            CASE FRQ.VAL[9,3] EQ 'M06'
                INT.PAY.PER = '2'
            CASE FRQ.VAL[9,3] EQ 'M04'
                INT.PAY.PER = '3'
            CASE FRQ.VAL[9,3] EQ 'M03'
                INT.PAY.PER = '4'
            CASE FRQ.VAL[9,3] EQ 'M02'
                INT.PAY.PER = '6'
            CASE FRQ.VAL[9,3] EQ 'M01'
                INT.PAY.PER = '12'
            CASE FRQ.VAL[9,3] EQ ''
                INT.PAY.PER = '99'
            CASE FRQ.VAL EQ MAT.VAL
                INT.PAY.PER = '99'
        END CASE
    END
RETURN

GET.RE.STAT.LINE:
*---------------*
    LINE.CONT = ''
    IF R.EB.CONTRACT.BALANCES THEN
        Y.ASSET.TYPE = 'CREDIT'
        Y.CONSOL.KEY = R.EB.CONTRACT.BALANCES<ECB.CONSOL.KEY>
        Y.CONSOL.PART = FIELD(Y.CONSOL.KEY,'.',1,16)
        Y.IN.CONSOL.KEY = Y.CONSOL.PART:'.':Y.ASSET.TYPE
        Y.VARIABLE = ''; Y.RPRTS = ''; Y.LINES = ''
        CALL RE.CALCUL.REP.AL.LINE(Y.IN.CONSOL.KEY,Y.RPRTS,Y.LINES,Y.VARIABLE)
        Y.LINE = Y.RPRTS:'.':Y.LINES
        CALL F.READ(FN.RE.STAT.REP.LINE,Y.LINE,R.LINE,F.RE.STAT.REP.LINE,REP.ERR)
        LINE.CONT = R.LINE<RE.SRL.DESC,1>
    END

    GOSUB REDO.AZACC
    IF R.REDO.AZACC.DESC NE '' THEN
        Y.ASSET.TYPE = R.REDO.AZACC.DESC<AZACC.ASSET.TYPE>
        Y.DESC = R.REDO.AZACC.DESC<AZACC.DESC>
        Y.CR.POS = ''
        IF LINE.CONT EQ '' OR Y.LINES EQ '9999' THEN
            LOCATE 'CREDIT' IN Y.ASSET.TYPE<1,1> SETTING Y.CR.POS THEN
                LINE.CONT = Y.DESC<1,Y.CR.POS>
            END
        END
    END
RETURN
*----------------------------------------------------------------------------
REDO.AZACC:
***********
    R.REDO.AZACC.DESC = ''; AZACC.ERR = ''; Y.ASSET.TYPE = ''; Y.DESC = ''
    CALL F.READ(FN.REDO.AZACC.DESC,REC.ID,R.REDO.AZACC.DESC,F.REDO.AZACC.DESC,AZACC.ERR)
RETURN

GET.L.AZ.DEP.NAME:
******************
*
    REG.L.AZ.DEP.NAME = ''; L.AZ.DEP.NAME.VAL = ''
    L.AZ.DEP.NAME.VAL = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.INV.FACILITY.POS>
    BEGIN CASE
        CASE L.AZ.DEP.NAME.VAL EQ 'DP' OR L.AZ.DEP.NAME.VAL EQ 'DX'
            REG.L.AZ.DEP.NAME = 221
        CASE L.AZ.DEP.NAME.VAL EQ 'CF'
            REG.L.AZ.DEP.NAME = 231
        CASE L.AZ.DEP.NAME.VAL EQ 'CP'
            REG.L.AZ.DEP.NAME = 237
    END CASE
*
RETURN
*----------------------------------------------------------------------------
PROCESS.LCY:
***********
* PACS00312508 / S
    RETURN.MSG = ''; R.AC1 = ''; CLIENT.TYPE = ''; YR.CUSTOMER = ''
    YFLD1 = Y.TODAY[7,2]:Y.TODAY[5,2]:Y.TODAY[1,4]
    RETURN.MSG<-1> = FMT(YFLD1,'L#8')
    RETURN.MSG<-1> = FMT(Y.PREV.ACCOUNT,'L#60')
    YT.REC.ID = REC.ID
    YR.CUSTOMER = R.CUSTOMER
    CALL DR.REG.GET.CUST.TYPE(YR.CUSTOMER,YT.REC.ID,CLIENT.TYPE,CUSTOMER.CODE)
    RETURN.MSG<-1> = FMT(CUSTOMER.CODE,'L#30')
    RETURN.MSG<-1> = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.CALIFICATION>,'L#10')
    RETURN.MSG<-1> = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.IRB>,'L#5')
*    RETURN.MSG<-1> = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.INV.FACILITY.POS>
    GOSUB GET.L.AZ.DEP.NAME
    RETURN.MSG<-1> = FMT(REG.L.AZ.DEP.NAME,'L#3')
    RETURN.MSG<-1> = FMT(REC.ID,'L#10')
    VAL.DATE = R.AZ.ACCOUNT<AZ.VALUE.DATE>
    YFLD8 = YAC.OPEN.DATE[7,2]:'/':YAC.OPEN.DATE[5,2]:'/':YAC.OPEN.DATE[1,4]
    RETURN.MSG<-1> = FMT(YFLD8,'L#10')
    MAT.DATE = R.AZ.ACCOUNT<AZ.MATURITY.DATE>
    Y.REGION = ''; YINT.ONLINE.VAL = '0'; Y.DAYS = ''
    IF LEN(VAL.DATE) EQ 8 AND LEN(MAT.DATE) EQ 8 THEN
        Y.DAYS = 'C'
        CALL CDD(Y.REGION,MAT.DATE,VAL.DATE,Y.DAYS)
    END
    YFLD9 =  ABS(Y.DAYS)
    RETURN.MSG<-1> = FMT(YFLD9,'R%5')
    YFLD10 = MAT.DATE[7,2]:'/':MAT.DATE[5,2]:'/':MAT.DATE[1,4]
    RETURN.MSG<-1> = FMT(YFLD10,'L#10')
    YFLD11 =  ABS(Y.DAYS)
    RETURN.MSG<-1> = FMT(YFLD11,'R%5')
    RTEM.EB.CONTRACT.BALANCES = ''; YINT.ONLINE.VAL = ''; YONLINE.VAL = ''
    IF R.ACCOUNT<AC.LOCAL.REF,L.AC.REINVESTED.POS> EQ 'YES' THEN
        GOSUB READ.INT.ACCT
        YONLINE.VAL = R.AC1<AC.OPEN.ACTUAL.BAL>
        YT.REC.ID = AC1.ID
        RTEM.EB.CONTRACT.BALANCES = R.EB.CONTRACT.BALANCES
        GOSUB GET.INT.PAY.AMT
        YINT.ONLINE.VAL = YONLINE.VAL + INTEREST.PAYABLE
        R.EB.CONTRACT.BALANCES = RTEM.EB.CONTRACT.BALANCES
    END
    ORIG.PRIN = R.AZ.ACCOUNT<AZ.PRINCIPAL>
    TOT.AMT = ORIG.PRIN + YINT.ONLINE.VAL
    TOT.AMT = FMT(TOT.AMT,'R2%15')
    RETURN.MSG<-1> = TOT.AMT
    YFLD13 = R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.BUY.PRICE>
    RETURN.MSG<-1> = FMT(YFLD13,'R4%8')
    RETURN.MSG<-1> = FMT(R.AZ.ACCOUNT<AZ.INTEREST.RATE>,'R2%6')
    RETURN.MSG<-1> = FMT(R.AZ.ACCOUNT<AZ.INTEREST.RATE>,'R2%6')
    YTYPE.INT.PAY = ''; RATE.TYPE = ''
    YTYPE.INT.PAY = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.TYPE.INT.PAY.POS>
    IF (YTYPE.INT.PAY EQ "Reinvested") THEN
        RATE.TYPE = 4
    END ELSE
        RATE.TYPE = 5
    END
    RETURN.MSG<-1> = FMT(RATE.TYPE,'L#2')
    YT.REC.ID = REC.ID
    GOSUB GET.INT.PAY.AMT
    RETURN.MSG<-1> = FMT(INTEREST.PAYABLE,'R4%15')
    GOSUB GET.INT.PAY.PERIOD
    RETURN.MSG<-1> = FMT(INT.PAY.PER,'R%2')
    RETURN.MSG<-1> = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.REBUY.RESELL>,'R%1')
    RETURN.MSG<-1> = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.REBUY.RESELL>,'R%1')
    RETURN.MSG<-1> = FMT('','L#10')     ;* Blank as per APAP
    GOSUB GET.RE.STAT.LINE
    RETURN.MSG<-1> = FMT(LINE.CONT,'L#30')
    RETURN.MSG<-1> = FMT('','L#3')      ;* Blank as per APAP
    RETURN.MSG<-1> = FMT(CLIENT.TYPE,'L#2')
    YORG.MAT.DATE = R.AZ.ACCOUNT<AZ.ORIG.MAT.DATE>
    IF RECORD.STAT EQ 'CLOSED' THEN
        YFLD25 = Y.TODAY[7,2]:'/':Y.TODAY[5,2]:'/':Y.TODAY[1,4]
        RETURN.MSG<-1> = FMT(YFLD25,'L#10')
    END ELSE
        RETURN.MSG<-1> = FMT('','L#10')
    END
    RETURN.MSG<-1> = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.CANCEL.PRICE>,'R4%15')
    GOSUB GET.CCY.CODE
    RETURN.MSG<-1> = FMT(CCY.CODE,'R%2')
    RETURN.MSG<-1> = FMT('','L#10')     ;* APAP doesn't have a revision date
    RETURN.MSG<-1> = FMT(R.DR.REG.RIEN6.PARAM<RIEN6.PARAM.REPAY.FREQ>,'R%3')
    GOSUB GET.CALC.BASIC
    RETURN.MSG<-1> = FMT(CALC.BASIS,'R%1')
    GOSUB GET.REL.CODE
    RETURN.MSG<-1> = FMT(REL.VAL,'L#2')
    CUS.INDUST = R.CUSTOMER<EB.CUS.LOCAL.REF,L.APAP.INDUSTRY.POS>
    RETURN.MSG<-1> = FMT(CUS.INDUST,'R%6')
    GOSUB GET.STATUS.VAL
    RETURN.MSG<-1> = FMT(STAT.VAL,'L#1')
*    RETURN.MSG<-1> = R.COMPANY(EB.COM.NAME.ADDRESS)<1,3>
    GOSUB GET.COMP.CODE
    RETURN.MSG<-1> = FMT(LOC.CODE,'R%6')
    RETURN.MSG<-1> = FMT(R.CUSTOMER<EB.CUS.LOCAL.REF,L.TIP.CLI.POS>,'R%3')
*
    YWORK.AMT = ''; YINT.ONLINE.VAL = ''; RTEMP.EB.CONTRACT.BALANCES = ''
    IF R.ACCOUNT<AC.LOCAL.REF,L.AC.REINVESTED.POS> EQ 'YES' THEN
        IF R.AC1 THEN
            YT.REC.ID = ''; YT.REC.ID = R.AZ.ACCOUNT<AZ.INTEREST.LIQU.ACCT>
            RTEMP.EB.CONTRACT.BALANCES = R.EB.CONTRACT.BALANCES
            GOSUB GET.INT.PAY.AMT
            YINT.ONLINE.VAL = YONLINE.VAL + INTEREST.PAYABLE
            RETURN.MSG<-1> = FMT(YINT.ONLINE.VAL,'R2%15')   ;* Field 36 in the mapping
            GOSUB GET.INT.LIQ.AC.GL.CODE
            RETURN.MSG<-1> = FMT(INT.LIQ.AC.GL.CODE,'L#30') ;* Field 37 in the mapping
            R.EB.CONTRACT.BALANCES = RTEMP.EB.CONTRACT.BALANCES
        END
    END ELSE
        RETURN.MSG<-1> = FMT('0','R2%15')         ;* Field 36 in the mapping
        RETURN.MSG<-1> = FMT('','L#30') ;* Field 37 in the mapping
    END
* PACS00312508 / E
    GOSUB GET.GL.CODE.50000
    RETURN.MSG<-1> = FMT(GL.CODE.50000,'L#30')    ;* Field 38 in the mapping
    GOSUB ADD.FIELD.39.43
    CHANGE @FM TO ',' IN RETURN.MSG
RETURN

ADD.FIELD.39.43:
****************
    IF YROLL.DTE THEN
        RETURN.MSG<-1> = FMT('V','L#2')
    END ELSE
        RETURN.MSG<-1> = FMT('V','L#2')
    END

    IF CUS.INDUST GE '651000' AND CUS.INDUST LE '672204' THEN
        RETURN.MSG<-1> = FMT('Y','L#1')
        IF CUS.INDUST EQ "651104" THEN
            RETURN.MSG<-1> = FMT('M','L#1')
        END
    END ELSE
        RETURN.MSG<-1> = FMT('M','L#1')
    END
    GOSUB GET.FIELD.41
    YFLD.CVAL = "NA"
    RETURN.MSG<-1> = FMT(YFLD.CVAL,'L#4')
    RETURN.MSG<-1> = FMT(YFLD.CVAL,'L#3')
RETURN

GET.FIELD.41:
*************
    YTERM.CHK = '0'
    BEGIN CASE
        CASE YFLD9 LE '15'
            YTERM.CHK = "110"
        CASE YFLD9 GE '16' AND YFLD9 LE '30'
            YTERM.CHK = "120"
        CASE YFLD9 GE '31' AND YFLD9 LE '60'
            YTERM.CHK = "130"
        CASE YFLD9 GE '61' AND YFLD9 LE '90'
            YTERM.CHK = "140"
        CASE YFLD9 GE '91' AND YFLD9 LE '180'
            YTERM.CHK = "150"
        CASE YFLD9 GE '181' AND YFLD9 LE '365'
            YTERM.CHK = "160"
        CASE YFLD9 GE '366' AND YFLD9 LE '730'
            YTERM.CHK = "170"
        CASE YFLD9 GE '731' AND YFLD9 LE '1095'
            YTERM.CHK = "180"
        CASE YFLD9 GE '1096' AND YFLD9 LE '1460'
            YTERM.CHK = "190"
        CASE YFLD9 GE '1461' AND YFLD9 LE '1825'
            YTERM.CHK = "200"
        CASE YFLD9 GE '1826'
            YTERM.CHK = "210"
    END CASE
    RETURN.MSG<-1> = FMT(YTERM.CHK,'R%5')
RETURN

READ.INT.ACCT:
**************
    R.AC1 = '';ACC1.ERR = ''; YH.ACINT.ID = ''; ERR.ACC1 = ''
    AC1.ID = R.AZ.ACCOUNT<AZ.INTEREST.LIQU.ACCT>
    CALL F.READ(FN.ACCOUNT,AC1.ID,R.AC1,F.ACCOUNT,ACC1.ERR)
    IF NOT(R.AC1) THEN
        YH.ACINT.ID = AC1.ID
        CALL EB.READ.HISTORY.REC(F.ACCOUNT.HST,YH.ACINT.ID,R.AC1,ERR.ACC1)
    END
RETURN

GET.GL.CODE.50000:
******************
    IF R.EB.CONTRACT.BALANCES THEN
        Y.ASSET.TYPE = '50000'
        Y.CONSOL.KEY = ''
        Y.CONSOL.KEY = R.EB.CONTRACT.BALANCES<ECB.CONSOL.KEY>
        Y.CONSOL.PART = FIELD(Y.CONSOL.KEY,'.',1,16)
        Y.IN.CONSOL.KEY = Y.CONSOL.PART:'.':Y.ASSET.TYPE
        Y.VARIABLE = ''; Y.RPRTS = ''; Y.LINES = ''
        CALL RE.CALCUL.REP.AL.LINE(Y.IN.CONSOL.KEY,Y.RPRTS,Y.LINES,Y.VARIABLE)
        Y.LINE = ''; R.LINE = ''; REP.ERR = ''
        Y.LINE = Y.RPRTS:'.':Y.LINES
        CALL F.READ(FN.RE.STAT.REP.LINE,Y.LINE,R.LINE,F.RE.STAT.REP.LINE,REP.ERR)
        IF R.LINE THEN
            GL.CODE.50000 = R.LINE<RE.SRL.DESC,1>
        END
    END

    R.REDO.AZACC.DESC = ''; AZACC.ERR = ''; Y.ASSET.TYPE = ''; Y.DESC = ''
    CALL F.READ(FN.REDO.AZACC.DESC,AC1.ID,R.REDO.AZACC.DESC,F.REDO.AZACC.DESC,AZACC.ERR)
    IF R.REDO.AZACC.DESC NE '' THEN
        Y.ASSET.TYPE = R.REDO.AZACC.DESC<AZACC.ASSET.TYPE>
        Y.DESC = R.REDO.AZACC.DESC<AZACC.DESC>
        Y.CR.POS = ''
        IF GL.CODE.50000 EQ '' OR Y.LINES EQ '9999' THEN
            LOCATE Y.ASSET.TYPE IN Y.ASSET.TYPE<1,1> SETTING Y.CR.POS THEN
                LINE.CONT = Y.DESC<1,Y.CR.POS>
            END
        END
    END
*
RETURN
*----------------------------------------------------------------------------
GET.INT.LIQ.AC.GL.CODE:
***********************
    R.EB.CONTRACT.BALANCES1 = ''; EB.CONTRACT.BALANCES.ERR = ''
    CALL F.READ(FN.EB.CONTRACT.BALANCES,AC1.ID,R.EB.CONTRACT.BALANCES1,F.EB.CONTRACT.BALANCES,EB.CONTRACT.BALANCES.ERR)
    IF R.EB.CONTRACT.BALANCES1 THEN
        Y.ASSET.TYPE = 'CREDIT'
        Y.CONSOL.KEY = ''
        Y.CONSOL.KEY = R.EB.CONTRACT.BALANCES1<ECB.CONSOL.KEY>
        Y.CONSOL.PART = FIELD(Y.CONSOL.KEY,'.',1,16)
        Y.IN.CONSOL.KEY = Y.CONSOL.PART:'.':Y.ASSET.TYPE
        Y.VARIABLE = ''; Y.RPRTS = ''; Y.LINES = ''
        CALL RE.CALCUL.REP.AL.LINE(Y.IN.CONSOL.KEY,Y.RPRTS,Y.LINES,Y.VARIABLE)
        Y.LINE = ''; R.LINE = ''; REP.ERR = ''
        Y.LINE = Y.RPRTS:'.':Y.LINES
        CALL F.READ(FN.RE.STAT.REP.LINE,Y.LINE,R.LINE,F.RE.STAT.REP.LINE,REP.ERR)
        IF R.LINE THEN
            INT.LIQ.AC.GL.CODE = R.LINE<RE.SRL.DESC,1>
        END
    END
    R.REDO.AZACC.DESC = ''; AZACC.ERR = ''; Y.ASSET.TYPE = ''; Y.DESC = ''
    CALL F.READ(FN.REDO.AZACC.DESC,AC1.ID,R.REDO.AZACC.DESC,F.REDO.AZACC.DESC,AZACC.ERR)
    IF R.REDO.AZACC.DESC NE '' THEN
        Y.ASSET.TYPE = R.REDO.AZACC.DESC<AZACC.ASSET.TYPE>
        Y.DESC = R.REDO.AZACC.DESC<AZACC.DESC>
        Y.CR.POS = ''
        IF INT.LIQ.AC.GL.CODE EQ '' OR Y.LINES EQ '9999' THEN
            LOCATE 'CREDIT' IN Y.ASSET.TYPE<1,1> SETTING Y.CR.POS THEN
                LINE.CONT = Y.DESC<1,Y.CR.POS>
            END
        END
    END
RETURN

GET.INT.PAY.AMT:
****************
    R.EB.CONTRACT.BALANCES = ''; EB.CONTRACT.BALANCES.ERR = ''
    OPEN.BAL = ''; CRED.MVMT = ''; DEBT.MVMT = ''; INTEREST.PAYABLE = ''; VAL.CODE = ''
    CALL F.READ(FN.EB.CONTRACT.BALANCES,YT.REC.ID,R.EB.CONTRACT.BALANCES,F.EB.CONTRACT.BALANCES,EB.CONTRACT.BALANCES.ERR)
    IF R.EB.CONTRACT.BALANCES THEN
        YTYPE.SYSDATE = R.EB.CONTRACT.BALANCES<ECB.TYPE.SYSDATE>
        YTYPE = DCOUNT(YTYPE.SYSDATE,@VM)
        YTOT.OPEN.BAL = ''
        FOR COUT = 1 TO YTYPE
            SYS.DATE = ''; YSYSTYPE = ''
            Y.TYPE.SYSDATE = R.EB.CONTRACT.BALANCES<ECB.TYPE.SYSDATE,COUT>
            YSYSTYPE = FIELD(Y.TYPE.SYSDATE,'-',1)
            SYS.DATE = FIELD(Y.TYPE.SYSDATE,'-',2)

            YOPEN.BALANCE = '' ; YCREDIT.MVMT = '' ; YAC.BAL  = ''; YDEBIT.MVMT = ''
            IF (YSYSTYPE EQ '50000' AND SYS.DATE LE Y.TODAY) THEN
                YOPEN.BALANCE = R.EB.CONTRACT.BALANCES<ECB.OPEN.BALANCE,COUT,1>
                YCREDIT.MVMT = R.EB.CONTRACT.BALANCES<ECB.CREDIT.MVMT,COUT,1>
                YDEBIT.MVMT = R.EB.CONTRACT.BALANCES<ECB.DEBIT.MVMT,COUT,1>
                YAC.BAL  = YOPEN.BALANCE + YCREDIT.MVMT + YDEBIT.MVMT
            END
            INTEREST.PAYABLE += YAC.BAL
        NEXT COUT
    END
RETURN

GET.COMP.CODE:
**************
    GOSUB CODE.INIT
*
    AZ.COMP = R.AZ.ACCOUNT<AZ.CO.CODE>
    LOCATE AZ.COMP IN COM.CODES1<1> SETTING COD.POS1 THEN
        LOC.CODE = '010101'
    END
*
    LOCATE AZ.COMP IN COM.CODES2<1> SETTING COD.POS2 THEN
        LOC.CODE = '060101'
    END
*
    LOCATE AZ.COMP IN COM.CODES3<1> SETTING COD.POS3 THEN
        LOC.CODE = '110104'
    END
*
    LOCATE AZ.COMP IN COM.CODES4<1> SETTING COD.POS4 THEN
        LOC.CODE = '120101'
    END
*
    LOCATE AZ.COMP IN COM.CODES5<1> SETTING COD.POS5 THEN
        LOC.CODE = '210101'
    END
*
    LOCATE AZ.COMP IN COM.CODES6<1> SETTING COD.POS6 THEN
        LOC.CODE = '210301'
    END
*
    LOCATE AZ.COMP IN COM.CODES7<1> SETTING COD.POS7 THEN
        LOC.CODE = '250101'
    END
*
    LOCATE AZ.COMP IN COM.CODES8<1> SETTING COD.POS8 THEN
        LOC.CODE = '320101'
    END
*
    LOCATE AZ.COMP IN COM.CODES9<1> SETTING COD.POS9 THEN
        LOC.CODE = '320201'
    END
*
    LOCATE AZ.COMP IN COM.CODES10<1> SETTING COD.POS10 THEN
        LOC.CODE = '320301'
    END
*
    LOCATE AZ.COMP IN COM.CODES11<1> SETTING COD.POS11 THEN
        LOC.CODE = '320601'
    END
*
    LOCATE AZ.COMP IN COM.CODES12<1> SETTING COD.POS12 THEN
        LOC.CODE = '320603'
    END
*
RETURN
*----------------------------------------------------------------------------
CODE.INIT:
**********
*
    COM.CODES1 = 'DO0010001':@FM:'DO0010002':@FM:'DO0010004':@FM:'DO0010005':@FM:'DO0010007':@FM:'DO0010008':@FM:'DO0010011':@FM:'DO0010012':@FM:'DO0010013':@FM:'DO0010015':@FM:'DO0010017':@FM:'DO0010019':@FM:'DO0010022':@FM:'DO0010023':@FM:'DO0010026':@FM:'DO0010027':@FM:'DO0010028':@FM:'DO0010031':@FM:'DO0010045':@FM:'DO0010047':@FM:'DO0010048':@FM:'DO0010051':@FM:'DO0010057':@FM:'DO0010060':@FM:'DO0010009':@FM:'DO0010050'       ;*PACS00309078
    COM.CODES2 = 'DO0010055'
    COM.CODES3 = 'DO0010056':@FM:'DO0010058'
    COM.CODES4 = 'DO0010059'
    COM.CODES5 = 'DO0010030':@FM:'DO0010049'
    COM.CODES6 = 'DO0010014'
    COM.CODES7 = 'DO0010038':@FM:'DO0010046':@FM:'DO0010053':@FM:'DO0010054'
    COM.CODES8 = 'DO0010003':@FM:'DO0010010':@FM:'DO0010020':@FM:'DO0010029':@FM:'DO0010033':@FM:'DO0010042':@FM:'DO0010044':@FM:'DO0010052'
    COM.CODES9 = 'DO0010018':@FM:'DO0010032'
    COM.CODES10 = 'DO0010021':@FM:'DO0010039'
    COM.CODES11 = 'DO0010035'
    COM.CODES12 = 'DO0010043'
*
    GOSUB COD.POS.INIT
RETURN

COD.POS.INIT:
*************
    COD.POS1 = ''; COD.POS2 = ''; COD.POS3 = ''; COD.POS4 = ''
    COD.POS5 = ''; COD.POS6 = ''; COD.POS7 = ''; COD.POS8 = ''
    COD.POS9 = ''; COD.POS10 = ''; COD.POS11 = ''; COD.POS12 = ''
RETURN

GET.STATUS.VAL:
***************
    STAT1 = ''; STAT2 = ''; STAT.VAL = ''; STAT2.CNT = ''
    STAT1 = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.AC.STATUS1.POS>
    STAT2 = R.AZ.ACCOUNT<AZ.LOCAL.REF,L.AC.STATUS2.POS>
    STAT2.CNT = DCOUNT(STAT2,@SM)

    IF STAT2.CNT GE 2 THEN
        GOSUB GET.STATUS.MULTIVAL
        RETURN
    END

    BEGIN CASE
        CASE STAT2 EQ 'DECEASED'
            STAT.VAL = 'F'
        CASE STAT2 EQ 'GARNISHMENT'
            STAT.VAL = 'E'
        CASE STAT2 EQ 'GUARANTEE.STATUS'
            STAT.VAL = 'G'
        CASE STAT1 EQ 'ACTIVE' OR STAT1 EQ '6MINACTIVE' OR STAT1 EQ ''
            STAT.VAL = 'A'
        CASE STAT1 EQ 'ABANDONED'
            STAT.VAL = 'B'
        CASE STAT1 EQ '3YINACTIVE'
            STAT.VAL = 'I'
    END CASE
RETURN

GET.STATUS.MULTIVAL:
********************
    D.POSN = ''; G.POSN = ''
    LOCATE 'DECEASED' IN STAT2<1,1,1> SETTING D.POSN THEN
        STAT.VAL = 'F'
    END ELSE
        LOCATE 'GARNISHMENT' IN STAT2<1,1,1> SETTING G.POSN THEN
            STAT.VAL = 'E'
        END ELSE
            STAT.VAL = 'G'
        END
    END
RETURN

GET.REL.CODE:
*************
    REL.CODE = R.CUSTOMER<EB.CUS.RELATION.CODE,1>
    PF.CODES = '302':@FM:'368':@FM:'369':@FM:'370':@FM:'371':@FM:'372':@FM:'402':@FM:'468':@FM:'469':@FM:'470':@FM:'471':@FM:'472'
    PG.CODES = '303':@FM:'373':@FM:'374':@FM:'375':@FM:'376':@FM:'377':@FM:'403':@FM:'473':@FM:'474':@FM:'475':@FM:'476':@FM:'477'
    G1.CODES = '304':@FM:'305':@FM:'306':@FM:'360':@FM:'361':@FM:'362':@FM:'363':@FM:'364':@FM:'365':@FM:'366':@FM:'367':@FM:'404':@FM:'405':@FM:'406':@FM:'460':@FM:'461':@FM:'462':@FM:'463':@FM:'464':@FM:'465':@FM:'466':@FM:'467'
    G2.CODES = '307':@FM:'308':@FM:'309':@FM:'310':@FM:'407':@FM:'408':@FM:'409':@FM:'410'
    CA.CODES = '330':@FM:'331':@FM:' 332':@FM:'333':@FM:'334':@FM:'335':@FM:'336':@FM:'337':@FM:'338':@FM:'339':@FM:'340':@FM:'341':@FM:'342':@FM:'343':@FM:'344':@FM:'345':@FM:'346':@FM:'347':@FM:'348':@FM:'430':@FM:'431':@FM:'432':@FM:'433':@FM:'434':@FM:'435':@FM:'436':@FM:'437':@FM:'438':@FM:'439':@FM:'440':@FM:'441':@FM:'442':@FM:'443':@FM:'444':@FM:'445':@FM:'446':@FM:'447':@FM:'448'
**** Change the below code by parameter the REL.CODE and REG.VAL. Use 1 LOCATE to fetch value
    BEGIN CASE
        CASE REL.CODE EQ 300 OR REL.CODE EQ 400
            REL.VAL = 'P1'
        CASE REL.CODE EQ 301 OR REL.CODE EQ 401
            REL.VAL = 'P2'
        CASE REL.CODE EQ 311 OR REL.CODE EQ 411
            REL.VAL = 'PR'
    END CASE
*
    LOCATE REL.CODE IN PF.CODES<1> SETTING PF.POS THEN
        REL.VAL = 'PF'
    END
*
    LOCATE REL.CODE IN PG.CODES<1> SETTING PF.POS THEN
        REL.VAL = 'PG'
    END
*
    LOCATE REL.CODE IN G1.CODES<1> SETTING PF.POS THEN
        REL.VAL = 'G1'
    END
*
    LOCATE REL.CODE IN G2.CODES<1> SETTING PF.POS THEN
        REL.VAL = 'G2'
    END
*
    LOCATE REL.CODE IN CA.CODES<1> SETTING PF.POS THEN
        REL.VAL = 'CA'
    END

    IF NOT(REL.VAL) THEN
        REL.VAL = 'NI'
    END
RETURN

GET.CALC.BASIC:
**************
    GRP.DT.ID = CAT.VAL:CCY.VAL
    CALL F.READ(FN.GROUP.DATE,GRP.DT.ID,R.GROUP.DATE,F.GROUP.DATE,GROUP.DATE.ERR)
    GRP.CR.DATE = R.GROUP.DATE<AC.GRD.CREDIT.GROUP.DATE>
    GCI.ID = CAT.VAL:CCY.VAL:GRP.CR.DATE
    CALL F.READ(FN.GROUP.CREDIT.INT,GCI.ID,R.GROUP.CREDIT.INT,F.GROUP.CREDIT.INT,GROUP.CREDIT.INT.ERR)
    IF R.GROUP.CREDIT.INT<IC.GCI.INTEREST.DAY.BASIS> EQ 'A' THEN
        CALC.BASIS = '0'
    END ELSE
        CALC.BASIS = '1'
    END
RETURN

GET.CCY.CODE:
************
*
    BEGIN CASE
        CASE CCY.VAL EQ LCCY
            CCY.CODE = '0'
        CASE CCY.VAL EQ 'USD'
            CCY.CODE = '1'
        CASE CCY.VAL EQ 'EUR'
            CCY.CODE = '2'
        CASE CCY.VAL EQ 'JPY'
            CCY.CODE = '3'
    END CASE
RETURN

END
