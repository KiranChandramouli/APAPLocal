SUBROUTINE DR.BLD.OPER.DIVISAS(CON.ID)
*-----------------------------------------------------------------------------
* Modification History :
* ----------------------
*   Date        Author             Modification Description
* 12-Sep-2014   V.P.Ashokkumar     PACS00318671 - Rewritten to create 2 reports
* 30-Sep-2014   V.P.Ashokkumar     PACS00318671 - compra and venta condition changed
* 08-May-2015   V.P.Ashokkumar     PACS00318671 - Changed the rate field to DEAL.RATE.
* 24-Jun-2015   Ashokkumar.V.P     PACS00466000 - Mapping changes - Fetch customer details to avoid blank.
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.FOREX
    $INSERT I_F.FUNDS.TRANSFER
    $INSERT I_F.TELLER
    $INSERT I_F.POS.MVMT.TODAY
    $INSERT I_BATCH.FILES
    $INSERT I_F.EB.LOOKUP
    $INSERT I_F.CUSTOMER
    $INSERT I_F.FT.TXN.TYPE.CONDITION
    $INSERT I_DR.BLD.OPER.DIVISAS

    GOSUB INIT
    GOSUB PROCESS
RETURN

PROCESS:
*********
    BEGIN CASE
        CASE CONTROL.LIST<1,1> EQ 'FOREX'
            GOSUB FOREX.PARA
        CASE CONTROL.LIST<1,1> EQ 'POS.MVMT.HST'
            GOSUB POS.MVMT.PARA
    END CASE
RETURN

POS.MVMT.PARA:
**************
    CALL F.READ(FN.POS.MVMT.HST,CON.ID,R.POS.MVMT.HIST,F.POS.MVMT.HST,ERR.POS.MVMT.HIST)
    IF NOT(R.POS.MVMT.HIST) THEN
        RETURN
    END

    YSYS.ID = R.POS.MVMT.HIST<PSE.SYSTEM.ID>
    IF YSYS.ID NE 'TT' AND YSYS.ID NE 'FT' THEN
        RETURN
    END

    Y.TT.CCY = R.POS.MVMT.HIST<PSE.CURRENCY>
    IF Y.TT.CCY EQ LCCY THEN
        RETURN
    END

    YTXN.CODE = R.POS.MVMT.HIST<PSE.TRANSACTION.CODE>
    YPOS.AMTLCY = R.POS.MVMT.HIST<PSE.AMOUNT.LCY>

    BEGIN CASE
        CASE YSYS.ID EQ 'TT'
            GOSUB MVMT.TT
        CASE YSYS.ID EQ 'FT'
            GOSUB MVMT.FT
    END CASE

    IF RET EQ 1 THEN
        RETURN
    END

    YCON.ID = ''; YCON.ID = "GRP1":CON.ID
    R.DR.OPER.DIVISAS.FILE = FLD1:'|':FLD2:'|':FLD3:'|':FLD4:'|':FLD5:'|':FLD6:'|':FLD7:'|':FLD8
    IF NOT(FLD1) THEN
        RETURN
    END
    GOSUB POP.BLD.DIVISAS
    YCON.ID = ''
    IF FLD4 EQ 'compra' THEN
        YCON.ID = "G3B":CON.ID
        R.DR.OPER.DIVISAS.FILE = FLD4:'|':YFLD2:'|':FLD6:'|':FLD5:'|':YFLD6:'|':FLD7
        GOSUB POP.BLD.DIVISAS
    END
    IF FLD4 EQ 'venta' THEN
        YCON.ID = "G3S":CON.ID
        R.DR.OPER.DIVISAS.FILE = FLD4:'|':YFLD2:'|':FLD6:'|':FLD5:'|':YFLD6:'|':FLD7
        GOSUB POP.BLD.DIVISAS
    END
RETURN

FOREX.PARA:
***********
    R.FOREX = ''; ERR.FOREX = ''; ERRH.FOREX = ''; YLEG.DEF = ''; YREC.STATUS = ''
    CALL F.READ(FN.FOREX,CON.ID,R.FOREX,F.FOREX,ERR.FOREX)
    IF NOT(R.FOREX) THEN
        CALL EB.READ.HISTORY.REC(F.FOREX.HST,CON.ID,R.FOREX,ERRH.FOREX)
    END
    YREC.STATUS = R.FOREX<FX.RECORD.STATUS>
    IF NOT(R.FOREX) OR YREC.STATUS EQ 'REVE' THEN
        RET = 1
        RETURN
    END
    YLEG.FX.VAL = R.FOREX<FX.LOCAL.REF,L.FX.LEGAL.ID.POS>
    YLEG.DEF = YLEG.FX.VAL
    FLD1 =  R.FOREX<FX.LOCAL.REF,L.FX.FXSN.NUM.POS>
    FLD2 = FIELD(YLEG.FX.VAL,'.',3)
    GOSUB GET.CUST.ID
    GOSUB GET.FX.FLDS4.TO.6.9
    GOSUB GET.FX.FLD.7
    R.DR.OPER.DIVISAS.FILE = FLD1:'|':FLD2:'|':FLD3:'|':FLD4:'|':FLD5:'|':FLD6:'|':FLD7:'|':FLD8
    IF NOT(FLD1) THEN
        RETURN
    END
    GOSUB POP.BLD.DIVISAS
    IF FLD4 EQ 'compra' THEN
        YCON.ID = "G3B":CON.ID
        R.DR.OPER.DIVISAS.FILE = FLD4:'|':YFLD2:'|':FLD6:'|':FLD5:'|':YFLD6:'|':FLD7
        GOSUB POP.BLD.DIVISAS
    END
    IF FLD4 EQ 'venta' THEN
        YCON.ID = "G3S":CON.ID
        R.DR.OPER.DIVISAS.FILE = FLD4:'|':YFLD2:'|':FLD6:'|':FLD5:'|':YFLD6:'|':FLD7
        GOSUB POP.BLD.DIVISAS
    END
RETURN

MVMT.FT:
*********
    YLEG.DEF = ''; YREC.STATUS = ''; YTXN.TYPE = ''
    FT.ID = R.POS.MVMT.HIST<PSE.OUR.REFERENCE>
    CALL F.READ(FN.FUNDS.TRANSFER,FT.ID,R.FT,F.FUNDS.TRANSFER,FUNDS.TRANSFER.ERR)
    IF NOT(R.FT) THEN
        CALL EB.READ.HISTORY.REC(F.FUNDS.TRANSFER.HST,FT.ID,R.FT,FUNDS.TRANSFER.ERR)
    END
    YREC.STATUS = R.FT<FT.RECORD.STATUS>
    IF NOT(R.FT) OR YREC.STATUS EQ 'REVE' THEN
        RET = 1
        RETURN
    END
    IF R.FT<FT.DEBIT.CURRENCY> EQ R.FT<FT.CREDIT.CURRENCY> THEN
        RET = 1
        RETURN
    END
    YTXN.TYPE = R.FT<FT.TRANSACTION.TYPE>
    YLEG.FT.VAL = R.FT<FT.LOCAL.REF,L.FT.LEGAL.ID.POS>
    YLEG.DEF = YLEG.FT.VAL
    FLD1 = R.FT<FT.LOCAL.REF,L.FT.FXSN.NUM.POS>
    IF YLEG.FT.VAL THEN
        FLD2 = FIELD(YLEG.FT.VAL,'.',3)
        YLEG.FT.TYPE = FIELD(YLEG.FT.VAL,'.',1)
        CUSTOMER.ID = FIELD(YLEG.FT.VAL,'.',2)
        Y.NATION = FIELD(YLEG.FT.VAL,".",4)
        IF YLEG.FT.TYPE EQ 'PASAPORTE' AND R.FT<FT.CREDIT.CUSTOMER> THEN
            Y.CUSTOMER.ID = R.FT<FT.CREDIT.CUSTOMER>
            GOSUB READ.CUSTOMER
        END
        GOSUB GET.FT.CUST.ID
    END ELSE
        ACCOUNT.ID = ''; CUST.TYPE = ''; CUST.IDEN = ''
        Y.CUSTOMER.ID = R.FT<FT.CHARGED.CUSTOMER>
        GOSUB READ.CUSTOMER
        CALL DR.REG.GET.CUST.TYPE(R.CUSTOMER,ACCOUNT.ID,CUST.TYPE,CUST.IDEN)
        GOSUB GET.CUST.NME.DET
        FLD3    = CUST.IDEN
    END
    GOSUB GET.FT.FLDS4.TO.6.8
    FLD7 = R.FT<FT.TREASURY.RATE>
RETURN

GET.FT.FLDS4.TO.6.8:
********************
    BEGIN CASE
        CASE R.FT<FT.DEBIT.CURRENCY> NE LCCY AND R.FT<FT.DEBIT.CURRENCY> NE '' AND YPOS.AMTLCY[1,1] EQ '-'
            FLD4 =  "compra"
            FLD5 = R.FT<FT.DEBIT.CURRENCY>
            FLD6 = R.FT<FT.AMOUNT.DEBITED>[4,99]
            EB.LOOKUP.ID = 'L.TT.FX.BUY.SRC':'*':R.FT<FT.LOCAL.REF,L.FT.FX.BUY.SRC.POS>
            GOSUB GET.EB.LOOKUP
            YFLD2 = YORG.LKID
            YFLD6 = R.FT<FT.CREDIT.CURRENCY>
        CASE R.FT<FT.CREDIT.CURRENCY> NE LCCY AND R.FT<FT.CREDIT.CURRENCY> NE '' AND YPOS.AMTLCY[1,1] NE '-'
            FLD4 =  "venta"
            FLD5 = R.FT<FT.CREDIT.CURRENCY>
            FLD6 = R.FT<FT.AMOUNT.CREDITED>[4,99]
            EB.LOOKUP.ID = 'L.TT.FX.SEL.DST':'*':R.FT<FT.LOCAL.REF,L.FT.FX.SEL.DST.POS>
            GOSUB GET.EB.LOOKUP
            YFLD2 = YORG.LKID
            YFLD6 = R.FT<FT.DEBIT.CURRENCY>
    END CASE

    ERR.FTTC = ''; R.FT.TXN.TYPE.CONDITION = ''; YTXN.COND = ''
    CALL F.READ(FN.FT.TXN.TYPE.CONDITION,YTXN.TYPE,R.FT.TXN.TYPE.CONDITION,F.FT.TXN.TYPE.CONDITION,ERR.FTTC)
    IF R.FT.TXN.TYPE.CONDITION AND NOT(YORG.LKID) THEN
        YTXN.COND = R.FT.TXN.TYPE.CONDITION<FT6.LOCAL.REF,L.FTTC.CHANNELS.POS>
        IF YTXN.COND EQ 'ARC' THEN
            FLD8 = "Pago Tarjeta de Cr√©dito Internacional"
            YFLD2 = "ARC"
        END
    END

    IF FLD5 NE LCCY AND YFLD6 EQ '' THEN
        YFLD6 = LCCY
    END
RETURN

GET.CUST.NME.DET:
*****************
    Y.L.CU.TIPO.CL =  R.CUSTOMER<EB.CUS.LOCAL.REF,L.CU.TIPO.CL.POS>
    IF Y.L.CU.TIPO.CL EQ "PERSONA FISICA" OR Y.L.CU.TIPO.CL EQ "CLIENTE MENOR" THEN
        FLD2    = R.CUSTOMER<EB.CUS.GIVEN.NAMES>
    END
    IF Y.L.CU.TIPO.CL EQ "PERSONA JURIDICA" THEN
        FLD2    = R.CUSTOMER<EB.CUS.NAME.1>:R.CUSTOMER<EB.CUS.NAME.2>
    END
RETURN

GET.FT.CUST.ID:
***************
    BEGIN CASE
        CASE YLEG.FT.TYPE EQ 'CEDULA'
            FLD3 = CUSTOMER.ID[1,3]:'-':CUSTOMER.ID[4,7]:'-':CUSTOMER.ID[11,1]
        CASE YLEG.FT.TYPE EQ 'RNC'
            FLD3 = CUSTOMER.ID[1,1]:'-':CUSTOMER.ID[2,2]:'-':CUSTOMER.ID[4,5]:'-':CUSTOMER.ID[9,1]
        CASE YLEG.FT.TYPE EQ 'PASAPORTE'
            IF NOT(CUS.ERR) THEN
                Y.NATION = R.CUSTOMER<EB.CUS.NATIONALITY>
            END
            FLD3 = Y.NATION:CUSTOMER.ID
    END CASE
RETURN

GET.CUST.DET:
*************
    ERR.CUSTOMER.L.CU.CIDENT = ''; R.CUSTOMER.L.CU.CIDENT = ''
    CALL F.READ(FN.CUSTOMER.L.CU.CIDENT,CUSTOMER.ID,R.CUSTOMER.L.CU.CIDENT,F.CUSTOMER.L.CU.CIDENT,ERR.CUSTOMER.L.CU.CIDENT)
    IF R.CUSTOMER.L.CU.CIDENT THEN
        Y.CUSTOMER.ID = R.CUSTOMER.L.CU.CIDENT
        RETURN
    END
    ERR.CUSTOMER.L.CU.RNC = ''; R.CUSTOMER.L.CU.RNC = ''
    CALL F.READ(FN.CUSTOMER.L.CU.RNC,CUSTOMER.ID,R.CUSTOMER.L.CU.RNC,F.CUSTOMER.L.CU.RNC,ERR.CUSTOMER.L.CU.RNC)
    IF R.CUSTOMER.L.CU.RNC THEN
        Y.CUSTOMER.ID = R.CUSTOMER.L.CU.RNC
        RETURN
    END
    ERR.CUSTOMER.L.CU.PASS.NAT = ''; R.CUSTOMER.L.CU.PASS.NAT = ''
    CALL F.READ(FN.CUSTOMER.L.CU.PASS.NAT,CUSTOMER.ID,R.CUSTOMER.L.CU.PASS.NAT,F.CUSTOMER.L.CU.PASS.NAT,ERR.CUSTOMER.L.CU.PASS.NAT)
    IF R.CUSTOMER.L.CU.PASS.NAT THEN
        Y.CUSTOMER.ID = R.CUSTOMER.L.CU.PASS.NAT
        RETURN
    END
RETURN

MVMT.TT:
*-------
    YLEG.DEF = ''; YREC.STATUS = ''; R.TT = ''; TELLER.ERR = ''
    TT.ID = R.POS.MVMT.HIST<PSE.OUR.REFERENCE>
    CALL F.READ(FN.TELLER,TT.ID,R.TT,F.TELLER,TELLER.ERR)
    IF NOT(R.TT) THEN
        CALL EB.READ.HISTORY.REC(F.TELLER.HST,TT.ID,R.TT,TELLER.ERR)
    END
    YREC.STATUS = R.TT<TT.TE.RECORD.STATUS>
    IF NOT(R.TT) OR YREC.STATUS EQ 'REVE' THEN
        RET = 1
        RETURN
    END
    IF R.TT<TT.TE.CURRENCY.1> EQ R.TT<TT.TE.CURRENCY.2> THEN
        RET = 1
        RETURN
    END
    YLEG.TT.VAL = R.TT<TT.TE.LOCAL.REF,L.TT.LEGAL.ID.POS>
    YLEG.TT.DOC.DESC = R.TT<TT.TE.LOCAL.REF,L.TT.DOC.DESC.POS>
    YLEG.DEF = YLEG.TT.VAL
    FLD1 = R.TT<TT.TE.LOCAL.REF,L.TT.FXSN.NUM.POS>
    BEGIN CASE
        CASE YLEG.TT.VAL
            FLD2 = FIELD(YLEG.TT.VAL,'.',3)
            YLEG.FT.TYPE = FIELD(YLEG.TT.VAL,'.',1)
            CUSTOMER.ID = FIELD(YLEG.TT.VAL,'.',2)
            Y.NATION = FIELD(YLEG.TT.VAL,".",4)
        CASE YLEG.TT.DOC.DESC
            FLD2 = R.TT<TT.TE.LOCAL.REF,L.TT.CLIENT.NME.POS>
            YLEG.FT.TYPE = YLEG.TT.DOC.DESC
            CUSTOMER.ID = R.TT<TT.TE.LOCAL.REF,L.TT.DOC.NUM.POS>
            IF NOT(FLD2) THEN
                GOSUB GET.CUST.DET
                GOSUB READ.CUSTOMER
                ACCOUNT.ID = ''; CUST.TYPE = ''; CUST.IDEN = ''
                CALL DR.REG.GET.CUST.TYPE(R.CUSTOMER,ACCOUNT.ID,CUST.TYPE,CUST.IDEN)
                FLD3    = CUST.IDEN
            END
    END CASE
    IF YLEG.FT.TYPE EQ 'PASAPORTE' AND R.TT<TT.TE.CUSTOMER.1> THEN
        Y.CUSTOMER.ID = R.TT<TT.TE.CUSTOMER.1>
        GOSUB READ.CUSTOMER
    END
    GOSUB GET.FT.CUST.ID
    GOSUB GET.TT.FLDS4.TO.6.8
    GOSUB GET.TT.FLD.7
RETURN

GET.TT.FLDS4.TO.6.8:
********************
    YMARKER = R.TT<TT.TE.DR.CR.MARKER>
    BEGIN CASE
        CASE R.TT<TT.TE.CURRENCY.1> NE LCCY AND YMARKER EQ 'CREDIT' AND YPOS.AMTLCY[1,1] NE '-'
            FLD4 = "venta"
            FLD5 = R.TT<TT.TE.CURRENCY.1>
            FLD6 = R.TT<TT.TE.AMOUNT.FCY.1>
            EB.LOOKUP.ID = 'L.TT.FX.SEL.DST':'*':R.TT<TT.TE.LOCAL.REF,L.TT.FX.SEL.DST.POS>
            GOSUB GET.EB.LOOKUP
            YFLD2 = YORG.LKID
            YFLD6 = R.TT<TT.TE.CURRENCY.2>
        CASE R.TT<TT.TE.CURRENCY.1> NE LCCY AND YMARKER EQ 'DEBIT' AND YPOS.AMTLCY[1,1] EQ '-'
            FLD4 = "compra"
            FLD5 = R.TT<TT.TE.CURRENCY.1>
            FLD6 = R.TT<TT.TE.AMOUNT.FCY.1>
            EB.LOOKUP.ID = 'L.TT.FX.BUY.SRC':'*':R.TT<TT.TE.LOCAL.REF,L.TT.FX.BUY.SRC.POS>
            GOSUB GET.EB.LOOKUP
            YFLD2 = YORG.LKID
            YFLD6 = R.TT<TT.TE.CURRENCY.2>
        CASE R.TT<TT.TE.CURRENCY.2> NE LCCY AND YMARKER EQ 'DEBIT' AND YPOS.AMTLCY[1,1] NE '-'
            FLD4 = "venta"
            FLD5 = R.TT<TT.TE.CURRENCY.2>
            FLD6 = R.TT<TT.TE.AMOUNT.FCY.2>
            EB.LOOKUP.ID = 'L.TT.FX.SEL.DST':'*':R.TT<TT.TE.LOCAL.REF,L.TT.FX.SEL.DST.POS>
            GOSUB GET.EB.LOOKUP
            YFLD2 = YORG.LKID
            YFLD6 = R.TT<TT.TE.CURRENCY.1>
        CASE R.TT<TT.TE.CURRENCY.2> NE LCCY AND YMARKER EQ 'CREDIT' AND YPOS.AMTLCY[1,1] EQ '-'
            FLD4 = "compra"
            FLD5 = R.TT<TT.TE.CURRENCY.2>
            FLD6 = R.TT<TT.TE.AMOUNT.FCY.2>
            EB.LOOKUP.ID = 'L.TT.FX.BUY.SRC':'*':R.TT<TT.TE.LOCAL.REF,L.TT.FX.BUY.SRC.POS>
            GOSUB GET.EB.LOOKUP
            YFLD2 = YORG.LKID
            YFLD6 = R.TT<TT.TE.CURRENCY.1>
    END CASE
    IF FLD5 NE LCCY AND YFLD6 EQ '' THEN
        YFLD6 = LCCY
    END
RETURN

GET.TT.FLD.7:
*************
    YTT.RATE = R.TT<TT.TE.DEAL.RATE>
    IF NOT(YTT.RATE) THEN
        YTT.RATE = R.TT<TT.TE.RATE.1>
    END
    IF NOT(YTT.RATE) THEN
        YTT.RATE = R.TT<TT.TE.RATE.2>
    END
    FLD7 = YTT.RATE
RETURN

GET.FX.FLDS4.TO.6.9:
********************
    BEGIN CASE
        CASE R.FOREX<FX.CURRENCY.BOUGHT> NE LCCY
            FLD4 = "venta"
            FLD5 = R.FOREX<FX.CURRENCY.BOUGHT>
            FLD6 = R.FOREX<FX.AMOUNT.BOUGHT>
            EB.LOOKUP.ID = 'L.TT.FX.SEL.DST':'*':R.FOREX<FX.LOCAL.REF,L.FX.FX.SEL.DST.POS>
            GOSUB GET.EB.LOOKUP
            YFLD2 = YORG.LKID
            YFLD6 = R.FOREX<FX.CURRENCY.SOLD>
        CASE R.FOREX<FX.CURRENCY.SOLD> NE LCCY
            FLD4 = "compra"
            FLD5 = R.FOREX<FX.CURRENCY.SOLD>
            FLD6 = R.FOREX<FX.AMOUNT.SOLD>
            EB.LOOKUP.ID = 'L.TT.FX.BUY.SRC':'*':R.FOREX<FX.LOCAL.REF,L.FX.FX.BUY.SRC.POS>
            GOSUB GET.EB.LOOKUP
            CON.ID := '.1'        ;* Not to overwrite in a cross currency FX
            YFLD2 = YORG.LKID
            YFLD6 = R.FOREX<FX.CURRENCY.BOUGHT>
    END CASE
RETURN

GET.EB.LOOKUP:
**************
    R.EB.LOOKUP = ''; EB.LOOKUP.ERR = ''; YORG.LKID = ''
    CALL F.READ(FN.EB.LOOKUP,EB.LOOKUP.ID,R.EB.LOOKUP,F.EB.LOOKUP,EB.LOOKUP.ERR)
    IF R.EB.LOOKUP THEN
        FLD8 = R.EB.LOOKUP<EB.LU.DESCRIPTION,2>
        YORG.LKID = R.EB.LOOKUP<EB.LU.LOOKUP.ID>
    END
    IF YLEG.DEF[1,13] EQ 'RNC.401000131' AND YORG.LKID EQ 'AAP' THEN
        YORG.LKID = 'ARB'
    END
RETURN

GET.FX.FLD.7:
*************
    IF R.FOREX<FX.DEAL.TYPE> EQ 'SP' THEN
        FLD7 = R.FOREX<FX.SPOT.RATE>
    END ELSE
        FLD7 = R.FOREX<FX.FORWARD.RATE>
    END
RETURN

GET.CUST.ID:
************
    YLEG.FX.TYPE = FIELD(YLEG.FX.VAL,'.',1)
    BEGIN CASE
        CASE YLEG.FX.TYPE EQ 'CEDULA'
            CUSTOMER.ID = FIELD(YLEG.FX.VAL,'.',2)
            FLD3 = CUSTOMER.ID[1,3]:'-':CUSTOMER.ID[4,7]:'-':CUSTOMER.ID[11,1]
        CASE YLEG.FX.TYPE EQ 'RNC'
            CUSTOMER.ID = FIELD(YLEG.FX.VAL,'.',2)
            FLD3 = CUSTOMER.ID[1,1]:'-':CUSTOMER.ID[2,2]:'-':CUSTOMER.ID[4,5]:'-':CUSTOMER.ID[9,1]
        CASE YLEG.FX.TYPE EQ 'PASAPORTE'
            CUSTOMER.ID = R.FOREX<FX.COUNTERPARTY>
            GOSUB READ.CUSTOMER
            IF NOT(CUS.ERR) THEN
                Y.NATION = R.CUSTOMER<EB.CUS.NATIONALITY>
            END ELSE
                Y.NATION = FIELD(YLEG.FX.VAL,".",4)
            END
            FLD3 = Y.NATION:FIELD(YLEG.FX.VAL,'.',2)
    END CASE
RETURN

POP.BLD.DIVISAS:
*--------------
    WRITE R.DR.OPER.DIVISAS.FILE TO F.DR.OPER.DIVISAS.FILE,YCON.ID ON ERROR NULL
RETURN

READ.CUSTOMER:
**************
    R.CUSTOMER = ''; CUS.ERR = ''
    CALL F.READ(FN.CUSTOMER,Y.CUSTOMER.ID,R.CUSTOMER,F.CUSTOMER,CUS.ERR)
RETURN

INIT:
*****
    FLD1 = ''; FLD2 = ''; FLD3 = ''; FLD4 = ''; FLD5 = ''; YSYS.ID = ''
    FLD6 = ''; FLD7 = ''; FLD8 = ''; R.FT = ''; FUNDS.TRANSFER.ERR = ''
    YLEG.FT.TYPE = ''; YLEG.FT.CUST.ID = ''; EB.LOOKUP.ID = ''; YLEG.FT.VAL = ''
    R.FOREX =  ''; R.DR.OPER.DIVISAS.FILE =  ''; YLEG.FX.TYPE = ''; YCON.ID = ''
    YLEG.FX.CUST.ID = ''; YLEG.FX.VAL = ''; R.TT = ''; TELLER.ERR = ''
    YLEG.TT.TYPE = ''; YLEG.TT.CUST.ID = ''; YTT.RATE = ''; EB.LOOKUP.ID = ''
    YLEG.TT.VAL = ''; R.POS.MVMT.HIST = '' ; ERR.POS.MVMT.HIST = ''; RET = 0
    YTXN.CODE = ''; YDATE = ''; R.REDO.FX.CCY.POSN = ''; ERR.REDO.FX.CCY.POSN = ''
    YFXC.TOTPOSN = ''; YFXC.BUYPOSN = ''; YFXC.SELPOSN = ''; YFXC.BUYAVG = ''
    YFXC.SEL.AVG = ''; YPOS.CCY = ''; YTOT.POS = ''; YL.FXC.TOTPOSN = ''
    YMARKER = ''; YFLD1 = ''; YFLD2 = ''; YFLD3 = ''; YFLD4 = ''; YFLD5 = ''
    YPOS.AMTLCY = ''
RETURN
END
